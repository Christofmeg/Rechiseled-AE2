import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'net.minecraftforge.gradle' version '6.+'
    id 'idea'
    id 'net.darkhax.curseforgegradle' version '1.+'
    id 'com.modrinth.minotaur' version '2.+'
}

archivesBaseName = "${mod_id}-forge-${forge_supported_versions}"

minecraft {
    mappings channel: mapping_channel, version: mapping_version

    runs {
        client {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Client'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                modClientRun {
                    source sourceSets.main
                    source project(":Common").sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Data'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            args '--mod', mod_id, '--all',
                    '--output', file('src/generated/resources/'),
                    '--existing', file('src/main/resources/'),
                    '--existing', file("${project(':Common').projectDir}/src/main/resources/"),
                    '--existing', file('src/generated/resources/')
            mods {
                modDataRun {
                    source sourceSets.main
                    source project(":Common").sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Server'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                modServerRun {
                    source sourceSets.main
                    source project(":Common").sourceSets.main
                }
            }
        }

    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources', 'src/generated/resources']
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    compileOnly project(":Common")

    // JEI
    if (project.hasProperty('jei_version')) {
        compileOnly(fg.deobf("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"))
        compileOnly(fg.deobf("mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}"))
        runtimeOnly(fg.deobf("mezz.jei:jei-${minecraft_version}-forge:${jei_version}"))
    }
    // CATALOGUE
    if (project.hasProperty('catalogue_version')) {
        implementation fg.deobf("curse.maven:catalogue-459701:${catalogue_version}")
    }
    // CONFIGURED
    if (project.hasProperty('configured_version')) {
        implementation fg.deobf("curse.maven:configured-457570:${configured_version}")
    }

    implementation fg.deobf("curse.maven:applied-energistics-2-223794:5117032") // https://www.curseforge.com/minecraft/mc-mods/applied-energistics-2/files
    implementation fg.deobf("curse.maven:rechiseled-558998:4872892") // https://www.curseforge.com/minecraft/mc-mods/rechiseled/files
    implementation fg.deobf("curse.maven:fusion-connected-textures-854949:5129294") // https://www.curseforge.com/minecraft/mc-mods/fusion-connected-textures/files
    implementation fg.deobf("curse.maven:supermartijn642s-config-lib-438332:4715408") // https://www.curseforge.com/minecraft/mc-mods/supermartijn642s-config-lib/files
    implementation fg.deobf("curse.maven:supermartijn642s-core-lib-454372:5102258") // https://www.curseforge.com/minecraft/mc-mods/supermartijn642s-core-lib/files

    implementation fg.deobf("curse.maven:jade-324717:4986594") // https://www.curseforge.com/minecraft/mc-mods/jade/files

}

processResources {
    from project(":Common").sourceSets.main.resources
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            forge_loader_version_range: forge_loader_version_range,
            mod_license: mod_license,
            issueTrackerURL: issueTrackerURL,
            minecraft_version: minecraft_version,
            forge_minecraft_version_range: forge_minecraft_version_range,
            forge_version: forge_version,
            forge_version_range: forge_version_range,
            catalogueItemIcon: catalogueItemIcon,

            mod_id: mod_id,
            version: version,
            display_name: display_name,
            displayURL: displayURL,
            mod_authors: mod_authors,
            mod_description: mod_description,
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

jar.finalizedBy('reobfJar')

tasks.register('publishCurseForge', TaskPublishCurseForge) {
    apiToken = file('../../API Tokens/rechiseledae2.md')
    def projectId = "${cf_id}".toString()
    def mainFile = upload(projectId, file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar"))
    mainFile.changelogType = 'markdown'
    mainFile.changelog = file('changelog.md')
    mainFile.addGameVersion('Client', 'Server')
    mainFile.releaseType = "${release_type}"
    "${forge_release_versions}".split(',').each {
        mainFile.addGameVersion("${it}")
    }
    mainFile.addRequirement("applied-energistics-2")
    mainFile.addRequirement("rechiseled")
}

modrinth {
    token = file('../../API Tokens/modrinth.md').text
    projectId = "${modrinth_id}"
    versionName = getArchivesBaseName() + "-" + getVersion()
    versionType = "${release_type}"
    changelog = file('changelog.md').text
    uploadFile = file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar")
    gameVersions = "${forge_release_versions}".split(",") as List<String>
    dependencies {
        required.project "ae2"
        required.project "rechiseled"
    }
}